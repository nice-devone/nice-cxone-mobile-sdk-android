plugins {
    id "android-application-conventions"
    id "android-ui-conventions"
    id "android-kotlin-conventions"
    id "ksp-conventions"
    id "koin-conventions"
    id "android-test-conventions"
    id "application-style-conventions"
    id 'com.google.gms.google-services'
    id "com.google.firebase.appdistribution"
    id 'com.google.firebase.crashlytics'
}

def storeVersion = branchVersion(version, project)

android {
    namespace 'com.nice.cxonechat.sample'

    signingConfigs {
        debug {
            storeFile file('../keystore.jks')
            storePassword 'qwerty135'
            keyAlias 'key0'
            keyPassword 'qwerty135'
        }
        release {
            storeFile file('../keystore.jks')
            storePassword 'qwerty135'
            keyAlias 'key0'
            keyPassword 'qwerty135'
        }
    }

    defaultConfig {
        applicationId "com.nice.cxonechat.sample"
        versionCode 1
        versionName storeVersion

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            versionNameSuffix '-debug'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            assets.srcDirs += rootProject.file("shared")
            java.srcDirs += new File(buildDir, "generated/ksp/main/kotlin")
        }
    }

    firebaseAppDistribution {
        artifactType = "APK"
        releaseNotesFile = "CHANGELOG.md"
    }

    packagingOptions {
        dex {
            useLegacyPackaging false
        }
    }
}

/* remove the superfluous files that happen to live in the shared assets directory */
android.applicationVariants.all { variant ->
    def task = variant.getMergeAssetsProvider().get()

    task.doLast {
        delete(fileTree(dir: task.outputDir, includes: ['*.md', '*.scheme.json']))
    }
}

dependencies {
    def retrofitVersion = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Initializer<T>
    implementation "androidx.lifecycle:lifecycle-common:2.7.0"
    implementation "androidx.startup:startup-runtime:1.1.1"

    // GoDaddy color picker
    def colorPickerVersion = "0.7.0"
    implementation "com.godaddy.android.colorpicker:compose-color-picker-android:$colorPickerVersion"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:32.7.1')
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-messaging-ktx'

    // Extended Emoji Support
    def emojiVersion = "1.4.0"
    implementation "androidx.emoji2:emoji2:$emojiVersion"
    implementation "androidx.emoji2:emoji2-bundled:$emojiVersion"

    // AsyncImage
    implementation("io.coil-kt:coil-compose:2.5.0")

    // CXOne Chat SDK
    implementation project(":chat-sdk-core")
    implementation project(":chat-sdk-ui")
    implementation project(':utilities')
    implementation project(':logger-android')

    // Crashlytics
    implementation("com.google.firebase:firebase-crashlytics-ktx")

    // LoginWithAmazon
    implementation fileTree(include: ['*.jar', '*.aar'], dir: rootProject.file("libs"))

    // Immutable annotations
    implementation "com.google.code.findbugs:jsr305:3.0.2"
}
