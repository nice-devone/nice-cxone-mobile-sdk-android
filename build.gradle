/*
 * Copyright (c) 2021-2025. NICE Ltd. All rights reserved.
 *
 * Licensed under the NICE License;
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    https://github.com/nice-devone/nice-cxone-mobile-sdk-android/blob/main/LICENSE
 *
 * TO THE EXTENT PERMITTED BY APPLICABLE LAW, THE CXONE MOBILE SDK IS PROVIDED ON
 * AN “AS IS” BASIS. NICE HEREBY DISCLAIMS ALL WARRANTIES AND CONDITIONS, EXPRESS
 * OR IMPLIED, INCLUDING (WITHOUT LIMITATION) WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE, NON-INFRINGEMENT, AND TITLE.
 */

import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektPlugin
import io.gitlab.arturbosch.detekt.report.ReportMergeTask

buildscript {
    configurations.configureEach {
        resolutionStrategy {
            // Android gradle plugin & root coverage plugin are using vulnerable dependencies
            eachDependency { details ->
                if (details.requested.group == 'io.netty' && details.requested.version < '4.1.125.Final') {
                    details.useVersion '4.1.125.Final'
                }
                if (details.requested.group == 'org.apache.commons' && details.requested.name == 'commons-compress') {
                    if (details.requested.version >= '1.21' && details.requested.version < '1.26.0') {
                        details.useVersion '1.26.0'
                    }
                }
            }

        }
    }
}

plugins {
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.kotlin.jvm) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.dokka) apply true
    alias(libs.plugins.maven.publish) apply false
    alias(libs.plugins.google.services) apply false
    alias(libs.plugins.androidx.safeargs) apply false
    alias(libs.plugins.detekt)
    alias(libs.plugins.rootcoverage) apply true
    alias(libs.plugins.semantic.version) apply false
    alias(libs.plugins.firebase.appdistribution) apply false
    alias(libs.plugins.firebase.crashlytics) apply false
    alias(libs.plugins.ksp) apply false
    alias(libs.plugins.compose.compiler) apply false
    alias(libs.plugins.kotlin.serialization) apply false
    alias(libs.plugins.build.config) apply false
    alias(libs.plugins.kotlinx.binary.compatibility.validator) apply true // Remove when Kotlin is upgraded to 2.2.0 or higher
}

subprojects {
    configurations.configureEach {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                // Android tests are using vulnerable dependency
                if (details.requested.group == 'com.google.protobuf' && details.requested.version < '3.25.5') {
                    details.useVersion '3.25.5'
                }
                // Android tests are using vulnerable dependency
                if (details.requested.group == 'io.grpc' && details.requested.version < '1.75.0') {
                    details.useVersion '1.75.0'
                }
                // Android tests are using vulnerable dependency
                if (details.requested.group == 'io.netty' && details.requested.version < '4.1.125.Final') {
                    details.useVersion '4.1.125.Final'
                }
                // Android gradle plugin & root coverage plugin are using vulnerable dependency
                if (details.requested.group == 'org.apache.commons' && details.requested.name == 'commons-compress') {
                    if (details.requested.version >= '1.21' && details.requested.version < '1.26.0') {
                        details.useVersion '1.26.0'
                    }
                }
            }
        }
    }
}

// Apply resolutionStrategy to all root project configurations
configurations.configureEach {
    resolutionStrategy {
        eachDependency { details ->
            if (details.requested.group == 'io.netty' && details.requested.name == 'netty-handler' && details.requested.version < '4.1.125.Final') {
                details.useVersion '4.1.125.Final'
            }
        }
    }
}

group = GROUP
version = "3.1.0" // Fallback version

allprojects {
    group = rootProject.group
    // Setup project version to override from gradle.properties or fallback version
    version = (rootProject.properties["VERSION_NAME"] ?: rootProject.version)
}

if (!rootProject.hasProperty("VERSION_NAME")) { // Disable this plugin if override is present, otherwise it will suppress it
    apply plugin: "com.dipien.semantic-version" // Plugin can be applied after version is defined
}


dependencies {
    dokka(project(":utilities"))
    dokka(project(":logger"))
    dokka(project(":logger-android"))
    dokka(project(":chat-sdk-core"))
    dokka(project(":chat-sdk-ui"))
}

dokka {
    moduleName.set("CXone Mobile SDK")
    dokkaPublications.html {
        includes.from(
                "README.md",
        )
        outputDirectory.set(project.file("dist"))
    }
}

// This task mirrors the tests that will eventually be performed in various github actions
// and can be used as a final check before submitting a PR.
tasks.register('precheck') {
    // Syntax Checks
    dependsOn(':chat-sdk-core:check')
    dependsOn(':chat-sdk-ui:check')
    dependsOn(':store:check')
    dependsOn(':store:lintVitalRelease')
    // Build Check
    dependsOn(':store:assembleRelease')
}

rootCoverage {
    generateXml true
    generateHtml false
    excludes = [
            "**/internal/model/**",
            "com/nice/cxonechat/sample/**",
            "com/nice/cxonechat/ui/**"
    ]
}

/**
 * Combines base version with branch and replaces all `/` with `-` from the final string.
 *
 * @param versionBase base which will be used for combining with branch.
 * @return version string based on base version and branch
 */
static String branchVersion(String versionBase, Project project) {
    if (versionBase == null) {
        return null
    }
    String branch = getGitCurrentBranch(project)
    return versionBase + "-" + branch.replaceAll("[/_]", "-")
}

static String getGitCurrentBranch(Project project) {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line ->
        project.logger.lifecycle("Current git branch: $line")
        branch = line
    }
    proc.err.eachLine { line -> project.logger.error("Unable to resolve git branch: $line") }
    proc.waitFor()
    return branch
}

tasks.register('cleanProject', Delete) {
    delete rootProject.layout.buildDirectory
}

tasks.register('reportMerge', ReportMergeTask) {
    output = rootProject.layout.buildDirectory.file("reports/detekt/merge.sarif")
}

subprojects {
    afterEvaluate {
        plugins.withType(DetektPlugin).configureEach {
            tasks.withType(Detekt).configureEach { detektTask ->
                finalizedBy(reportMerge)

                reportMerge.configure { mergeTask ->
                    mergeTask.input.from(detektTask.sarifReportFile)
                }
            }
        }
    }
}

apiValidation {
    /**
     * Packages that are excluded from public API dumps even if they
     * contain public API.
     */
    ignoredPackages += [
            "com.nice.cxonechat.internal",
            "com.nice.cxonechat.ui.composable", // Composable classes contain generated code that is not part of the public API
            "org.koin.ksp.generated",
    ]

    /**
     * Classes (fully qualified) that are excluded from public API dumps even if they
     * contain public API.
     */
    ignoredClasses += [
            "com.nice.cxonechat.core.BuildConfig",
            "com.nice.cxonechat.ui.storage.TemporaryFileProvider",
            "com.nice.cxonechat.ui.screen.ComposableSingletons\$ThreadListScreenKt",
            "com.nice.cxonechat.ui.screen.ComposableSingletons\$OfflineScreenKt",
            "com.nice.cxonechat.ui.screen.ComposableSingletons\$ChatErrorScreenKt",
            "com.nice.cxonechat.ui.screen.ComposableSingletons\$ChatDialogScreenKt",
    ]

    /**
     * Sub-projects that are excluded from API validation
     */
    ignoredProjects += [
            "store",
            "cxone-detekt-rules"
    ]

    /**
     * Set of annotations that exclude API from being public.
     * Typically, it is all kinds of `@InternalApi` annotations that mark
     * effectively private API that cannot be actually private for technical reasons.
     */
    nonPublicMarkers += [
            "com.nice.cxonechat.InternalApiAnnotation",
            "androidx.compose.ui.tooling.preview.PreviewLightDark",
            "androidx.compose.ui.tooling.preview.Preview",
    ]

    /**
     * Flag to programmatically disable compatibility validator
     */
    validationDisabled = false

    /**
     * A path to a subdirectory inside the project root directory where dumps should be stored.
     */
    apiDumpDirectory = "api"
}
