/*
 * Copyright (c) 2021-2024. NICE Ltd. All rights reserved.
 *
 * Licensed under the NICE License;
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    https://github.com/nice-devone/nice-cxone-mobile-sdk-android/blob/main/LICENSE
 *
 * TO THE EXTENT PERMITTED BY APPLICABLE LAW, THE CXONE MOBILE SDK IS PROVIDED ON
 * AN “AS IS” BASIS. NICE HEREBY DISCLAIMS ALL WARRANTIES AND CONDITIONS, EXPRESS
 * OR IMPLIED, INCLUDING (WITHOUT LIMITATION) WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE, NON-INFRINGEMENT, AND TITLE.
 */

pluginManagement {
    repositories {
        gradlePluginPortal()
        google() {
            content {
                includeGroupByRegex "com.android.*"
                includeGroupByRegex "androidx.*"
                includeGroup "android.arch.lifecycle"
                includeGroup "android.arch.core"
                includeGroupByRegex "com.google.*"
            }
        }
        mavenCentral()
    }
}

plugins {
    id 'com.android.settings' version "$androidGradlePluginVersion"
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google() {
            content {
                includeGroupByRegex "com.android.*"
                includeGroupByRegex "androidx.*"
                includeGroup "android.arch.lifecycle"
                includeGroup "android.arch.core"
                includeGroupByRegex "com.google.*"
            }
        }
        mavenCentral()
        exclusiveContent {
            forRepository {
                //noinspection ForeignDelegate
                maven {
                    url 'https://jitpack.io'
                    content {
                        includeModule("com.github.jeziellago", "Markwon")
                        includeModule("com.github.jeziellago", "compose-markdown")
                    }
                }
            }
            filter {
                includeGroup("com.github.jeziellago")
            }
        }
    }
    versionCatalogs {
        create("libs") {
            version("androidGradlePluginVersion", "$androidGradlePluginVersion")
        }
    }
}

rootProject.name = "cxone-chat-sdk"

android {
    compileSdk 34
    minSdk 24
    execution {
        profiles {
            high {
                r8 {
                    jvmOptions += ["-Xms2048m", "-Xmx8192m", "-XX:+HeapDumpOnOutOfMemoryError"]
                    runInSeparateProcess true
                }
            }
            low {
                r8 {
                    jvmOptions += ["-Xms256m", "-Xmx2048m", "-XX:+HeapDumpOnOutOfMemoryError"]
                    runInSeparateProcess true
                }
            }
            ci {
                r8.runInSeparateProcess false
            }
        }
        defaultProfile "low"
    }

}

include ':chat-sdk-core'
include ':chat-sdk-ui'
include ':cxone-detekt-rules'
include ':store'
include ':utilities'
include ':logger'
include ':logger-android'
