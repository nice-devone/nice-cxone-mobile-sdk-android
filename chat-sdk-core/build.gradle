plugins {
    id "android-conventions"
    id "kotlin-conventions"
    id "docs-conventions"
    id "test-conventions"
    id "style-conventions"
    id "publish-conventions"
}

group GROUP
version (project.properties["VERSION_NAME"] ?: "0.1.0-SNAPSHOT")

android {
    namespace 'com.nice.cxonechat'

    defaultConfig {
        consumerProguardFiles "consumer-rules.pro"
        versionName version
        buildConfigField 'String', 'VERSION_NAME', "\"$version\""
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

afterEvaluate {
    publishing {
        publications {
            debug(MavenPublication) {
                artifact bundleDebugAar
                artifact sourceReleaseJar { classifier = "sources" }
                artifact javaDocReleaseJar { classifier = "documentation" }

                artifactId "$POM_ARTIFACT_ID-debug"

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    def dependencies = new ArrayList<Dependency>()
                    dependencies.addAll(configurations.api.allDependencies)
                    dependencies.addAll(configurations.implementation.allDependencies)
                    dependencies.each {
                        if (it.group != null && it.name != null && it.version != null) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.9.0"
    implementation 'com.neovisionaries:nv-websocket-client:2.14'
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
}
